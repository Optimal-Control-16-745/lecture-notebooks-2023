{
 "cells": [
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "import Pkg; Pkg.activate(@__DIR__); Pkg.instantiate()"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "using LinearAlgebra\n",
    "using PyPlot\n",
    "using ForwardDiff\n",
    "using Ipopt\n",
    "using MathOptInterface\n",
    "const MOI = MathOptInterface;"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "Nx = 8     # number of state\n",
    "Nu = 2     # number of controls\n",
    "Tfinal = 4.4 # final time\n",
    "h = 0.1 #10 hz\n",
    "Nm = 5 #number of steps in each mode\n",
    "Nt = Int(ceil(Tfinal/h)+1)   # number of time steps\n",
    "Nmodes = Int(ceil(Nt/Nm))\n",
    "thist = Array(range(0,h*(Nt-1), step=h));\n",
    "n_nlp = Nx*Nt + Nu*(Nt-1) # number of decision variables\n",
    "c_init_inds = 1:Nx\n",
    "c_term_inds = (c_init_inds[end]+1):(c_init_inds[end]+Nx)\n",
    "c_dyn_inds = (c_term_inds[end]+1):(c_term_inds[end]+Nx*(Nt-1))\n",
    "c_stance_inds = (c_dyn_inds[end]+1):(c_dyn_inds[end]+Int(ceil(Nmodes/2)*Nm))\n",
    "c_length_inds = (c_stance_inds[end]+1):(c_stance_inds[end]+Nt)\n",
    "m_nlp = c_length_inds[end]"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "#Hopper Dynamics\n",
    "g = 9.81\n",
    "m1 = 5.0 #body mass\n",
    "m2 = 1.0 #foot mass\n",
    "ℓ_min = 0.5 #minimum length\n",
    "ℓ_max = 1.5 #maximum length\n",
    "\n",
    "function flight_dynamics(x,u)\n",
    "    M = Diagonal([m1 m1 m2 m2])\n",
    "    \n",
    "    r1 = x[1:2]\n",
    "    r2 = x[3:4]\n",
    "    v = x[5:8]\n",
    "    \n",
    "    ℓ1 = (r1[1]-r2[1])/norm(r1-r2)\n",
    "    ℓ2 = (r1[2]-r2[2])/norm(r1-r2)\n",
    "    B = [ℓ1  ℓ2;\n",
    "         ℓ2 -ℓ1;\n",
    "        -ℓ1 -ℓ2;\n",
    "        -ℓ2  ℓ1]\n",
    "    \n",
    "    v̇ = [0; -g; 0; -g] + M\\(B*u)\n",
    "    \n",
    "    ẋ = [v; v̇]\n",
    "end\n",
    "\n",
    "function stance_dynamics(x,u)\n",
    "    m1 = 5.0 #body mass\n",
    "    m2 = 1.0 #foot mass\n",
    "    M = Diagonal([m1 m1 m2 m2])\n",
    "    g = 9.81\n",
    "    \n",
    "    r1 = x[1:2]\n",
    "    r2 = x[3:4]\n",
    "    v = x[5:8]\n",
    "    \n",
    "    ℓ1 = (r1[1]-r2[1])/norm(r1-r2)\n",
    "    ℓ2 = (r1[2]-r2[2])/norm(r1-r2)\n",
    "    B = [ℓ1  ℓ2;\n",
    "         ℓ2 -ℓ1;\n",
    "         0   0;\n",
    "         0   0]\n",
    "    \n",
    "    v̇ = [0; -g; 0; 0] + M\\(B*u)\n",
    "    \n",
    "    ẋ = [v; v̇]\n",
    "end\n",
    "\n",
    "function flight_dynamics_rk4(x,u)\n",
    "    #RK4 integration with zero-order hold on u\n",
    "    f1 = flight_dynamics(x, u)\n",
    "    f2 = flight_dynamics(x + 0.5*h*f1, u)\n",
    "    f3 = flight_dynamics(x + 0.5*h*f2, u)\n",
    "    f4 = flight_dynamics(x + h*f3, u)\n",
    "    return x + (h/6.0)*(f1 + 2*f2 + 2*f3 + f4)\n",
    "end\n",
    "\n",
    "function stance_dynamics_rk4(x,u)\n",
    "    #RK4 integration with zero-order hold on u\n",
    "    f1 = stance_dynamics(x, u)\n",
    "    f2 = stance_dynamics(x + 0.5*h*f1, u)\n",
    "    f3 = stance_dynamics(x + 0.5*h*f2, u)\n",
    "    f4 = stance_dynamics(x + h*f3, u)\n",
    "    return x + (h/6.0)*(f1 + 2*f2 + 2*f3 + f4)\n",
    "end\n",
    "\n",
    "function jump_map(x)\n",
    "    #Assume the foot experiences inelastic collisions\n",
    "    xn = [x[1:6]; 0.0; 0.0]\n",
    "    return xn\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "# Cost weights\n",
    "Q = Diagonal([1.0*ones(4); 1.0*ones(4)]);\n",
    "R = 0.001;\n",
    "Qn = Q"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "function stage_cost(x,u,k)\n",
    "    return 0.5*((x-xref[:,k])'*Q*(x-xref[:,k])) + 0.5*(u-uref)'*R*(u-uref)\n",
    "end\n",
    "function terminal_cost(x)\n",
    "    return 0.5*((x-xref[:,end])'*Qn*(x-xref[:,end]))\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "function cost(ztraj)\n",
    "    z = reshape(ztraj[1:(end-Nx)],Nx+Nu,Nt-1)\n",
    "    xtraj = [z[1:Nx,:] ztraj[end-(Nx-1):end]]\n",
    "    utraj = z[(Nx+1):(Nx+Nu),:]\n",
    "    J = 0.0\n",
    "    for k = 1:(Nt-1)\n",
    "        J += stage_cost(xtraj[:,k],utraj[:,k],k)\n",
    "    end\n",
    "    J += terminal_cost(xtraj[:,end])\n",
    "    return J\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "#Reference Trajectory\n",
    "uref = [m1*g; 0.0]\n",
    "xref = zeros(Nx,Nt)\n",
    "xref[1,:] .= LinRange(-1.0,1.0,Nt)\n",
    "xref[2,:] .= 1.0 .+ 0.5.*sin.(2*pi/10.0*(0:(Nt-1)));\n",
    "xref[3,:] .= LinRange(-1.0,1.0,Nt)\n",
    "xref[5,2:end-1] .= (2.0/Tfinal)*ones(Nt-2)\n",
    "xref[7,2:end-1] .= (2.0/Tfinal)*ones(Nt-2);"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "plot(xref[2,:])"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "function dynamics_constraint!(c,ztraj)\n",
    "    d = reshape(view(c,c_dyn_inds),Nx,Nt-1)\n",
    "    z = reshape(ztraj[1:(end-Nx)],Nx+Nu,Nt-1)\n",
    "    xtraj = [z[1:Nx,:] ztraj[end-(Nx-1):end]]\n",
    "    utraj = z[(Nx+1):(Nx+Nu),:]\n",
    "    for k = 1:(Nmodes-1)\n",
    "        if mod(k,2) == 1\n",
    "            for j = 1:Nm\n",
    "                s = (k-1)*Nm + j\n",
    "                d[:,s] = stance_dynamics_rk4(xtraj[:,s],utraj[:,s]) - xtraj[:,s+1]\n",
    "            end\n",
    "        else\n",
    "            for j = 1:(Nm-1)\n",
    "                s = (k-1)*Nm + j\n",
    "                d[:,s] = flight_dynamics_rk4(xtraj[:,s],utraj[:,s]) - xtraj[:,s+1]\n",
    "            end\n",
    "            s = k*Nm\n",
    "            d[:,s] = jump_map(flight_dynamics_rk4(xtraj[:,s],utraj[:,s])) - xtraj[:,s+1]\n",
    "        end\n",
    "    end\n",
    "    if mod(Nmodes,2) == 1\n",
    "        for j = 1:(Nm-1)\n",
    "            s = (Nmodes-1)*Nm + j\n",
    "            d[:,s] = stance_dynamics_rk4(xtraj[:,s],utraj[:,s]) - xtraj[:,s+1]\n",
    "        end\n",
    "    else\n",
    "        for j = 1:(Nm-1)\n",
    "            s = (Nmodes-1)*Nm + j\n",
    "            d[:,s] = flight_dynamics_rk4(xtraj[:,s],utraj[:,s]) - xtraj[:,s+1]\n",
    "        end\n",
    "    end\n",
    "    \n",
    "    return nothing\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "function stance_constraint!(c,ztraj)\n",
    "    d = view(c,c_stance_inds)\n",
    "    z = reshape(ztraj[1:(end-Nx)],Nx+Nu,Nt-1)\n",
    "    xtraj = [z[1:Nx,:] ztraj[(end-(Nx-1)):end]]\n",
    "    t = 1\n",
    "    for k = 1:Nmodes\n",
    "        if mod(k,2) == 1\n",
    "            for j = 1:Nm\n",
    "                s = (k-1)*Nm + j\n",
    "                d[t] = xtraj[4,s]\n",
    "                t += 1\n",
    "            end\n",
    "        end\n",
    "            \n",
    "    end\n",
    "    return nothing\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "function length_constraint!(c,ztraj)\n",
    "    d = view(c,c_length_inds)\n",
    "    z = reshape(ztraj[1:(end-Nx)],Nx+Nu,Nt-1)\n",
    "    xtraj = [z[1:Nx,:] ztraj[(end-(Nx-1)):end]]\n",
    "    for k = 1:Nt\n",
    "        d[k] = norm(xtraj[1:2,k] - xtraj[3:4,k])\n",
    "    end\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "function con!(c,ztraj)\n",
    "    c[c_init_inds] .= ztraj[1:Nx] - xref[:,1] #initial state constraint\n",
    "    c[c_term_inds] .= ztraj[(end-(Nx-1)):end] - xref[:,end] #terminal state constraint\n",
    "    @views dynamics_constraint!(c,ztraj)\n",
    "    @views stance_constraint!(c,ztraj)\n",
    "    @views length_constraint!(c,ztraj)\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "struct ProblemMOI <: MOI.AbstractNLPEvaluator\n",
    "    n_nlp::Int\n",
    "    m_nlp::Int\n",
    "    idx_ineq\n",
    "    obj_grad::Bool\n",
    "    con_jac::Bool\n",
    "    sparsity_jac\n",
    "    sparsity_hess\n",
    "    primal_bounds\n",
    "    constraint_bounds\n",
    "    hessian_lagrangian::Bool\n",
    "end\n",
    "\n",
    "function ProblemMOI(n_nlp,m_nlp;\n",
    "        idx_ineq=c_length_inds,\n",
    "        obj_grad=true,\n",
    "        con_jac=true,\n",
    "        sparsity_jac=sparsity_jacobian(n_nlp,m_nlp),\n",
    "        sparsity_hess=sparsity_hessian(n_nlp,m_nlp),\n",
    "        primal_bounds=primal_bounds(n_nlp),\n",
    "        constraint_bounds=constraint_bounds(m_nlp,idx_ineq=idx_ineq),\n",
    "        hessian_lagrangian=false)\n",
    "\n",
    "    ProblemMOI(n_nlp,m_nlp,\n",
    "        idx_ineq,\n",
    "        obj_grad,\n",
    "        con_jac,\n",
    "        sparsity_jac,\n",
    "        sparsity_hess,\n",
    "        primal_bounds,\n",
    "        constraint_bounds,\n",
    "        hessian_lagrangian)\n",
    "end\n",
    "\n",
    "function primal_bounds(n)\n",
    "    x_l = -Inf*ones(n)\n",
    "    x_u = Inf*ones(n)\n",
    "    return x_l, x_u\n",
    "end\n",
    "\n",
    "function constraint_bounds(m; idx_ineq=(1:0))\n",
    "    c_l = zeros(m)\n",
    "    c_l[idx_ineq] .= ℓ_min\n",
    "\n",
    "    c_u = zeros(m)\n",
    "    c_u[idx_ineq] .= ℓ_max\n",
    "    return c_l, c_u\n",
    "end\n",
    "\n",
    "function row_col!(row,col,r,c)\n",
    "    for cc in c\n",
    "        for rr in r\n",
    "            push!(row,convert(Int,rr))\n",
    "            push!(col,convert(Int,cc))\n",
    "        end\n",
    "    end\n",
    "    return row, col\n",
    "end\n",
    "\n",
    "function sparsity_jacobian(n,m)\n",
    "\n",
    "    row = []\n",
    "    col = []\n",
    "\n",
    "    r = 1:m\n",
    "    c = 1:n\n",
    "\n",
    "    row_col!(row,col,r,c)\n",
    "\n",
    "    return collect(zip(row,col))\n",
    "end\n",
    "\n",
    "function sparsity_hessian(n,m)\n",
    "\n",
    "    row = []\n",
    "    col = []\n",
    "\n",
    "    r = 1:m\n",
    "    c = 1:n\n",
    "\n",
    "    row_col!(row,col,r,c)\n",
    "\n",
    "    return collect(zip(row,col))\n",
    "end\n",
    "\n",
    "function MOI.eval_objective(prob::MOI.AbstractNLPEvaluator, x)\n",
    "    cost(x)\n",
    "end\n",
    "\n",
    "function MOI.eval_objective_gradient(prob::MOI.AbstractNLPEvaluator, grad_f, x)\n",
    "    ForwardDiff.gradient!(grad_f,cost,x)\n",
    "    return nothing\n",
    "end\n",
    "\n",
    "function MOI.eval_constraint(prob::MOI.AbstractNLPEvaluator,g,x)\n",
    "    con!(g,x)\n",
    "    return nothing\n",
    "end\n",
    "\n",
    "function MOI.eval_constraint_jacobian(prob::MOI.AbstractNLPEvaluator, jac, x)\n",
    "    ForwardDiff.jacobian!(reshape(jac,prob.m_nlp,prob.n_nlp), con!, zeros(prob.m_nlp), x)\n",
    "    return nothing\n",
    "end\n",
    "\n",
    "function MOI.features_available(prob::MOI.AbstractNLPEvaluator)\n",
    "    return [:Grad, :Jac]\n",
    "end\n",
    "\n",
    "MOI.initialize(prob::MOI.AbstractNLPEvaluator, features) = nothing\n",
    "MOI.jacobian_structure(prob::MOI.AbstractNLPEvaluator) = prob.sparsity_jac\n",
    "\n",
    "function solve(x0,prob::MOI.AbstractNLPEvaluator;\n",
    "        tol=1.0e-6,c_tol=1.0e-6,max_iter=10000)\n",
    "    x_l, x_u = prob.primal_bounds\n",
    "    c_l, c_u = prob.constraint_bounds\n",
    "\n",
    "    nlp_bounds = MOI.NLPBoundsPair.(c_l,c_u)\n",
    "    block_data = MOI.NLPBlockData(nlp_bounds,prob,true)\n",
    "\n",
    "    solver = Ipopt.Optimizer()\n",
    "    solver.options[\"max_iter\"] = max_iter\n",
    "    solver.options[\"tol\"] = tol\n",
    "    solver.options[\"constr_viol_tol\"] = c_tol\n",
    "\n",
    "    x = MOI.add_variables(solver,prob.n_nlp)\n",
    "\n",
    "    for i = 1:prob.n_nlp\n",
    "        MOI.add_constraint(solver, x[i], MOI.LessThan(x_u[i]))\n",
    "        MOI.add_constraint(solver, x[i], MOI.GreaterThan(x_l[i]))\n",
    "        MOI.set(solver, MOI.VariablePrimalStart(), x[i], x0[i])\n",
    "    end\n",
    "\n",
    "    # Solve the problem\n",
    "    MOI.set(solver, MOI.NLPBlock(), block_data)\n",
    "    MOI.set(solver, MOI.ObjectiveSense(), MOI.MIN_SENSE)\n",
    "    MOI.optimize!(solver)\n",
    "\n",
    "    # Get the solution\n",
    "    res = MOI.get(solver, MOI.VariablePrimal(), x)\n",
    "\n",
    "    return res\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "#Guess\n",
    "xguess = xref + 0.1*randn(Nx,Nt)\n",
    "uguess = kron(ones(Nt-1)', uref) + 0.1*randn(Nu,Nt-1)\n",
    "z0 = [reshape([xguess[:,1:(Nt-1)]; uguess],(Nx+Nu)*(Nt-1),1); xguess[:,end]];"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "prob = ProblemMOI(n_nlp,m_nlp)\n",
    "z_sol = solve(z0,prob) # solve"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "z = reshape(z_sol[1:(end-Nx)],Nx+Nu,Nt-1)\n",
    "xtraj = [z[1:Nx,:] z_sol[end-(Nx-1):end]]\n",
    "utraj = z[(Nx+1):(Nx+Nu),:];"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "plot(thist,xtraj[2,:])\n",
    "plot(thist,xtraj[4,:])"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "plot(thist,xtraj[1,:])\n",
    "plot(thist,xtraj[3,:])"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "plot(thist[1:end-1],utraj[1,:])"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "plot(thist[1:end-1],utraj[2,:])"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "#Set up visualizer\n",
    "using Colors\n",
    "using CoordinateTransformations\n",
    "using GeometryBasics\n",
    "using MeshCat\n",
    "using Rotations\n",
    "using StaticArrays\n",
    "\n",
    "# Kinematics\n",
    "kinematics(q) = [q[3], q[4]]\n",
    "\n",
    "# Visualization\n",
    "function visualize!(vis, q, Δt)\n",
    "\n",
    "    # body radius\n",
    "    r_body = 0.2\n",
    "\t# foot radius\n",
    "    r_foot = 0.1\n",
    "\n",
    "\t# leg width\n",
    "    r_leg = 0.5 * r_foot\n",
    "\n",
    "\t# set background to white\n",
    "\tsetvisible!(vis[\"/Background\"], true)\n",
    "    setprop!(vis[\"/Background\"], \"top_color\", RGBA(1.0, 1.0, 1.0, 1.0))\n",
    "    setprop!(vis[\"/Background\"], \"bottom_color\", RGBA(1.0, 1.0, 1.0, 1.0))\n",
    "    setvisible!(vis[\"/Axes\"], false)\n",
    "\n",
    "\t# create body\n",
    "    setobject!(vis[\"body\"], Sphere(Point3f0(0),\n",
    "        convert(Float32, r_body)),\n",
    "        MeshPhongMaterial(color = RGBA(0, 1, 0, 1.0)))\n",
    "\n",
    "\t# create foot\n",
    "    setobject!(vis[\"foot\"], Sphere(Point3f0(0),\n",
    "        convert(Float32, r_foot)),\n",
    "        MeshPhongMaterial(color = RGBA(1.0, 165.0 / 255.0, 0, 1.0)))\n",
    "\n",
    "# \t# create leg\n",
    "    n_leg = 100\n",
    "    for i = 1:n_leg\n",
    "        setobject!(vis[\"leg$i\"], Sphere(Point3f0(0),\n",
    "            convert(Float32, r_leg)),\n",
    "            MeshPhongMaterial(color = RGBA(0, 0, 0, 1.0)))\n",
    "    end\n",
    "\n",
    "    p_leg = [zeros(3) for i = 1:n_leg]\n",
    "\n",
    "\t# animation\n",
    "    anim = MeshCat.Animation(convert(Int, floor(1.0 / Δt)))\n",
    "\n",
    "    for t = 1:length(q)\n",
    "        p_body = [q[t][1], 0.0, q[t][2]]\n",
    "        p_foot = [kinematics(q[t])[1], 0.0, kinematics(q[t])[2]]\n",
    "\n",
    "        dir = [q[t][3] - q[t][1]; q[t][4] - q[t][2]]\n",
    "        dir = dir ./ norm(dir)\n",
    "        r_range = range(0, stop = sqrt((q[t][1] - q[t][3])^2.0 + (q[t][2] - q[t][4])^2.0), length = n_leg)\n",
    "        for i = 1:n_leg\n",
    "            p_leg[i] = [q[t][1] + r_range[i] * dir[1], 0.0, q[t][2] + r_range[i] * dir[2]]\n",
    "        end\n",
    "        \n",
    "        z_shift = [0.0; 0.0; r_foot]\n",
    "\n",
    "        MeshCat.atframe(anim, t) do\n",
    "            settransform!(vis[\"body\"], Translation(p_body + z_shift))\n",
    "            settransform!(vis[\"foot\"], Translation(p_foot + z_shift))\n",
    "\n",
    "            for i = 1:n_leg\n",
    "                settransform!(vis[\"leg$i\"], Translation(p_leg[i] + z_shift))\n",
    "            end\n",
    "        end\n",
    "    end\n",
    "\n",
    "    MeshCat.setanimation!(vis, anim)\n",
    "end\n",
    "\n",
    "vis = Visualizer()\n",
    "render(vis)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "q = [SVector{4}(x[1:4]) for x in eachcol(xtraj)];\n",
    "visualize!(vis, q, h)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": []
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.6.7",
   "language": "julia",
   "name": "julia-1.6"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.6.7"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 4
}
